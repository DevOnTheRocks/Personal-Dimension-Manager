plugins {
    id "java"
    id "idea"
    id "signing"
    id "ninja.miserable.blossom" version "1.0.1"
    id "org.spongepowered.plugin" version "0.8.1"
    id "com.qixalite.spongestart2" version "4.0.0"
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id "io.franzbecker.gradle-lombok" version "1.14"
}

group = "rocks.gameonthe.pdm"
version = "${major}.${minor}.${patch}-${api}-${suffix}"
description = "Personal Dim Manager helps manage player & team dims."

if (System.getenv().BUILD_NUMBER != null) {
    version += "-${System.getenv().BUILD_NUMBER}"
}

compileJava {
    setSourceCompatibility(JavaVersion.VERSION_1_8)
    setTargetCompatibility(JavaVersion.VERSION_1_8)
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    jcenter()
    maven {
        name = "Sponge"
        url = "http://repo.spongepowered.org/maven"
    }
    maven {
        name = "Forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "Nucleus"
        url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
    maven {
        name = "bStats"
        url = "http://repo.bstats.org/content/repositories/releases/"
    }
    maven {
        name = "LuckPerms"
        url = "https://repo.lucko.me/"
    }
}

dependencies {
    compile "org.spongepowered:spongeapi:${spongeapi}"
    compile "me.ryanhamshire:griefprevention:${griefprevention}:api"
    compile "io.github.nucleuspowered:nucleus-api:${nucleus}"
    compile "org.bstats:bstats-sponge:${bstats}"
    compile "org.flywaydb:flyway-core:${flyaway}"
    runtimeOnly "me.ryanhamshire:griefprevention:${griefprevention}"
    //compile fileTree(dir: "libs", include: "*.jar")
}

shadowJar {
    dependencies {
        include(dependency("org.bstats:bstats-sponge:${bstats}"))
        include(dependency("org.flywaydb:flyway-core:${flyaway}"))
    }
}

lombok {
    version = "1.18.4"
}

blossom {
    def pluginInfo = "src/main/java/rocks/gameonthe/pdm/PluginInfo.java"

    replaceToken "@NAME@", project.name, pluginInfo
    replaceToken "@VERSION@", project.version, pluginInfo
    replaceToken "@DESCRIPTION@", project.description, pluginInfo
}

spongestart {
    online "false"
    minecraft "1.12.2"
    forgeServerFolder "${rootProject.projectDir}/run/API7/"
}

signing {
    required {Project.hasProperty("signing.keyId")}
    sign configurations.archives, configurations.shadow
}

build.dependsOn(shadowJar)